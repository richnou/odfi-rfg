package provide osys::rfg::xmlgenerator 1.0.0
package require Itcl 3.4
package require odfi::common
package require odfi::list 2.0.0

namespace eval osys::rfg::xmlgenerator {



    itcl::class XMLGenerator {

        public variable registerFile 

        constructor cRegisterFile {

            ## Init
            #########
            set registerFile $cRegisterFile
        }

        public method produce args {


            ## Create Special Stream 
            set out [odfi::common::newStringChannel]

            odfi::common::println "<RegisterFile name=\"[$registerFile name]\">"  $out 
            odfi::common::printlnIndent

            ## Write Groups 
            $registerFile onEachGroup {
                writeGroup $out $it
            }

            ## Write Registers 
            $registerFile onEachRegister {
                writeRegister $out $it
            }

            odfi::common::printlnOutdent
            odfi::common::println "</RegisterFile>"  $out 

        

            ## Read  form special stream and return 
            flush $out
            set res [read $out]
            close $out
            return $res



        }


        public method writeGroup {out group} {

            odfi::common::println "<Group name=\"[$group name]\">"  $out 
            odfi::common::printlnIndent

            ## Write Groups 
            $group onEachGroup {
                writeGroup $out $it
            }

            ## Write Registers 
            $group onEachRegister {
                writeRegister $out $it
            }

            odfi::common::printlnOutdent
            odfi::common::println "</Group>"  $out 


        }

        public method writeRegister {out register} {

            odfi::common::println "<Register name=\"[$register name]\">"  $out 
            odfi::common::printlnIndent

            ## Write Fields
            $register onEachField {
                writeField $out $it
            }

            odfi::common::printlnOutdent
            odfi::common::println "</Register>"  $out 


        }

        public method writeField {out field} {

            ## Reset 
            set reset "reset=\"[$field reset]\""

            ## Output 
            ##odfi::common::println "<Field name=\"[$field name]\"  width=\"[$field width]\" $reset [join $rightsString]/>"  $out 
            odfi::common::println "<Field name=\"[$field name]\"  width=\"[$field width]\" $reset >"  $out 
            odfi::common::printlnIndent
            $field onEachAttributes {
                    writeAttributes $out $it        
            }
            odfi::common::printlnOutdent
            odfi::common::println "</Field>" $out
        }

        public method writeAttributes {out attributes} {
                       
            odfi::common::println "<Attributes for=\"[$attributes name]\">"  $out
            odfi::common::printlnIndent
            foreach element [$attributes attr_list] { ## write each attribute
               if {[llength $element] == 2} {
                    odfi::common::println "<Attribute name=\"[lindex $element 0]\"> [lindex $element 1] </Attribute>"  $out
               } else {
                    odfi::common::println "<Attribute name=\"$element\"/>"  $out
               }
            }
            odfi::common::printlnOutdent
            odfi::common::println "</Attributes>" $out
        }

    }


}
